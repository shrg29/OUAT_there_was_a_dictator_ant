~ start
do GameState.low = 0.8
do GameState.high = 1.2

if GameState.recruited_ants.has("Anthony"):
	if GameState.ant_number_goal - GameState.recruited_ants.size() > 0:
		Anthony: I am ready! Let's recruit {{GameState.ant_number_goal - GameState.recruited_ants.size()}} more.
	else:
		Anthony: I think we have enough! Let's overthrow the queen!
	=> resolve
elif GameState.has_met_anthony == false:
	set GameState.has_met_anthony = true
	Anthony: Hi Sara, I'm so scared! The queen wants to force us into war!
	- I am trying to recruit enough of us to overthrow her.
		Anthony: That sounds great, but we need to be intimidating.
		Anthony: Can you bring me a weapon, like {{GameState.anthony_demand_amount}} {{GameState.anthony_demand_item.item_name}}?
		=> quest

	- What should we do?
		Anthony: You are popular here, maybe we can overthrow her?
		- That could work. But we will need to be intimidating.
			Anthony: Can you bring me a weapon, like {{GameState.anthony_demand_amount}} {{GameState.anthony_demand_item.item_name}}?
			=> quest
		- Are you crazy?
			Anthony: What other choice is there?
			- You may be right. But we will need to be intimidating.
				Anthony: Can you bring me a weapon, like {{GameState.anthony_demand_amount}} {{GameState.anthony_demand_item.item_name}}?
				=> quest
			- I give up. Maybe we get lucky and survive the war.
				set GameState.failed_game = true
				=> resolve
		- I am reporting you to the queen, traitor!
			set GameState.failed_game = true
			=> resolve

~ quest
do QuestManager.add_quest(GameState.anthony_quest)
if GameState.count_item(GameState.anthony_demand_item) >= GameState.anthony_demand_amount:
	- Here you go (Give him {{GameState.anthony_demand_amount}} {{GameState.anthony_demand_item.item_name}}).
		do GameState.give_item(GameState.anthony_demand_item, GameState.anthony_demand_amount)
		do GameState.recruit_ant("Anthony")
		do QuestManager.remove_quest(GameState.anthony_quest)
		do AudioManager.play_sfx("task_completed")
		Anthony: We need to recruit {{GameState.ant_number_goal - 2}} more to stand a chance. Go!
		=> resolve
	- Maybe later.
		=> resolve
else:
	- I will go look for one.
		=> resolve
	- Maybe later.
		=> resolve

~ resolve
set GameState.current_state = GameState.state.WALKING
=> END
