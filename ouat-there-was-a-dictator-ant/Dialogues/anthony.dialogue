~ start
do GameState.low = 0.8
do GameState.high = 1.2

if GameState.recruited_ants.has("Anthony"):
	Anthony: I am ready to overthrow the queen!
	=> resolve
elif GameState.has_met_anthony == false:
	set GameState.has_met_anthony = true
	Anthony: Hi Sara, I'm so scared! The queen wants to force us into war!
	- I am trying to recruit enough of us to overthrow her.
		Anthony: That sounds great, but we need to be intimidating.
		Anthony: Can you bring me a weapon, like {{GameState.anthony_demand_amount}} {{GameState.anthony_demand_item.item_name}}?
		=> quest

	- What should we do?
		Anthony: You are popular here, maybe we can overthrow her?
		- That could work I suppose. But we will need to be intimidating.
			Anthony: Can you bring me a weapon, like {{GameState.anthony_demand_amount}} {{GameState.anthony_demand_item.item_name}}?
			=> quest
		- Are you crazy?
			Anthony: What other choice is there?
			- Hm, I suppose you are right. But we will need to be intimidating.
				Anthony: Can you bring me a weapon, like {{GameState.anthony_demand_amount}} {{GameState.anthony_demand_item.item_name}}?
				=> quest
			- I think we should just give up. Or maybe we get lucky and win and survive the war.
				set GameState.failed_game = true
				=> resolve
		- I am reporting you to the queen, traitor!
			set GameState.failed_game = true
			=> resolve

~ quest
if GameState.count_item(GameState.anthony_demand_item) >= GameState.anthony_demand_amount:
	- Here you go (Give him {{GameState.anthony_demand_amount}} {{GameState.anthony_demand_item.item_name}}).
		do GameState.recruit_ant("Anthony")
		do AudioManager.play_sfx("task_completed")
	- Maybe later.
else:
	- I will go look for one.
	- Maybe later.

~ resolve
set GameState.current_state = GameState.state.WALKING
=> END
